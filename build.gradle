buildscript {
    repositories {
        mavenLocal()
        jcenter()
        maven {
            url 'http://repo.enonic.net/public'
        }
    }

    dependencies {
        classpath "com.enonic.xp:gradle-plugin:${xpVersion}"
        classpath 'com.eriwen:gradle-js-plugin:1.12.1'
        classpath 'com.eriwen:gradle-css-plugin:1.11.1'
        classpath "com.github.robfletcher:compass-gradle-plugin:2.0.5"
        classpath "com.github.jruby-gradle:jruby-gradle-plugin:0.1.12" // Required to build on Windows
    }
}

//apply plugin: 'idea'
apply plugin: 'com.enonic.xp.app'
//apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.eriwen.gradle.js'
apply plugin: 'css'
apply plugin: "com.github.robfletcher.compass"

app {
    name = project.appName
    displayName = project.displayName
    vendorName = 'Enonic AS'
    vendorUrl = 'http://enonic.com'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'http://repo.enonic.net/public'
    }
}

dependencies {
    include "com.enonic.xp:lib-content:${xpVersion}"
    include "com.enonic.xp:lib-portal:${xpVersion}"
    include "com.enonic.xp:lib-thymeleaf:${xpVersion}"
    include "com.enonic.lib:util:1.1.1"
    include "com.enonic.lib:menu:1.2.0"
    compass "rubygems:breakpoint:2.5.0"
    compass "rubygems:compass-normalize:1.5"
    compass "rubygems:sass-globbing:1.1.1"
}

publishing {
  publications {
    mavenJava( MavenPublication ) {
      from components.java
    }
  }
  repositories {
    maven {
      url 'http://repo.enonic.net/artifactory/public'
    }
  }
}

// Declare your sources
javascript.source {
    custom {
        js {
            srcDir "src/main/resources/assets/js"
            include "*.js"
            //exclude "*.min.js"
        }
    }
    lib {
        js {
            srcDir "src/main/resources/assets/lib"
            include "**/*.js"
            //exclude "*.min.js"
        }
    }
    vendor {
        js {
            srcDir "src/main/resources/assets/js/vendor"
            include "*.js"
        }
    }
    jquery {
        js {
            srcDir "src/main/resources/assets/js/vendor/jquery"
            include "*.js"
        }
    }
    controllers {
        js {
            srcDir "src/main/resources/site/parts"
            include "**/*.js"
        }
    }
}

jshint {
    tasks.jshint.source = javascript.source.custom.js.files + javascript.source.lib.js.files
    tasks.jshint.dest = file("${buildDir}/jshint.out")
    tasks.jshint.reporter = 'checkstyle'
    tasks.jshint.outputToStdOut = true
}

minifyJs {
    source = javascript.source.custom.js.files
    dest = file("${buildDir}/resources/main/assets/js/custom.min.js")
    closure {
        warningLevel = 'QUIET'
    }
}

// Configure the built-in task
combineJs {
    source = javascript.source.jquery.js.files + javascript.source.vendor.js.files + javascript.source.lib.js.files + javascript.source.custom.js.files
    dest = file("${buildDir}/resources/main/assets/js/all.js")
}

requireJs {
    source = javascript.source.custom.js.files
    dest = file("${buildDir}/resources/main/assets/js/main-built.js")
    requirejs.buildprofile = new File('src/main/resources/assets/js/build.js')
}

compass {
    cssDir = file("${buildDir}/resources/main/assets/css")
    sassDir = file("src/main/resources/assets/css")
}

jar.dependsOn += compassCompile
jar.dependsOn += combineJs