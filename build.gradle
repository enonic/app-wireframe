buildscript {
    repositories {
        jcenter()
        maven {
            url 'http://repo.enonic.net/public'
        }
    }

    dependencies {
        classpath 'com.enonic.xp.gradle:gradle-plugin:1.0.3'
        classpath 'com.eriwen:gradle-js-plugin:1.12.1'
        classpath "com.github.robfletcher:compass-gradle-plugin:2.0.5"
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'idea'
apply plugin: 'com.enonic.xp.gradle.module'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.eriwen.gradle.js'
apply plugin: "com.github.robfletcher.compass"


version = '1.0.0-SNAPSHOT'
group = 'com.enonic.module'

module {
    displayName = 'Enonic XP Wireframe Module'
}

publishing {
  publications {
    mavenJava( MavenPublication ) {
      from components.java
    }
  }
  repositories {
    maven {
      url 'http://repo.enonic.net/artifactory/public'
    }
  }
}

// Declare your sources
javascript.source {
    custom {
        js {
            srcDir "src/main/resources/cms/assets/js"
            include "*.js"
            //exclude "*.min.js"
        }
    }
    util {
            js {
                srcDir "src/main/resources/cms/assets/js/lib/util"
                include "*.js"
                //exclude "*.min.js"
            }
        }
    vendor {
        js {
            srcDir "src/main/resources/cms/assets/js/vendor"
            include "*.js"
        }
    }
    jquery {
        js {
            srcDir "src/main/resources/cms/assets/js/vendor/jquery"
            include "*.js"
        }
    }
    controllers {
        js {
            srcDir "src/main/resources/cms/parts"
            include "**/*.js"
        }
    }

}

jshint {
    tasks.jshint.source = javascript.source.custom.js.files + javascript.source.util.js.files
    tasks.jshint.dest = file("${buildDir}/jshint.out")
    tasks.jshint.reporter = 'checkstyle'
    tasks.jshint.outputToStdOut = true
}

minifyJs {
    source = javascript.source.custom.js.files
    dest = file("${buildDir}/resources/main/cms/assets/js/custom.min.js")
    closure {
        warningLevel = 'QUIET'
    }
}


// Configure the built-in task
combineJs {
    source = javascript.source.jquery.js.files + javascript.source.vendor.js.files + javascript.source.util.js.files + javascript.source.custom.js.files
    dest = file("${buildDir}/resources/main/cms/assets/js/all.js")
}


requireJs {
    source = javascript.source.custom.js.files
    dest = file("${buildDir}/resources/main/cms/assets/js/main-built.js")
    requirejs.buildprofile = new File('src/main/resources/cms/assets/js/build.js')
}

compass {
    cssDir = file("${buildDir}/resources/main/cms/assets/css")
    sassDir = file("src/main/resources/cms/assets/css")
}

dependencies {
  compass "rubygems:breakpoint:2.5.0"
  compass "rubygems:compass-normalize:1.5"
  compass "rubygems:sass-globbing:1.1.1"
}


jar.dependsOn += compassCompile
jar.dependsOn += combineJs
